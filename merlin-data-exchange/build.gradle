plugins {
    id 'wtmp.deps-conventions'
    id 'wtmp.java-conventions'
    id 'wtmp.publishing-conventions'
}

configurations {
    testRuntimeClasspath.extendsFrom(runtimeClasspath)
    windows_x64
    linux_x64
}

dependencies {
    implementation libs.jackson.dataformat.xml
    implementation libs.jackson.dataformat.csv
    implementation libs.jackson.datatype.jsr310

    implementation libs.rmadev
    implementation libs.hec.rma
    implementation libs.hec.lookup
    implementation libs.hec.core
    implementation libs.hec.data
    implementation libs.hec.lib
    implementation libs.merlin.web.client

    runtimeOnly libs.flogger.system.backend

    implementation libs.hec.service.annotations

    windows_x64 "mil.army.usace.hec:javaHeclib:${libs.versions.hec.javaHeclib.get()}-win-x86_64@zip"
    linux_x64 "mil.army.usace.hec:javaHeclib:${libs.versions.hec.javaHeclib.get()}-linux-x86_64@zip"

    annotationProcessor libs.hec.service.annotations
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'merlin-data-exchange'
            from components.java
        }
    }
}

tasks.register("extractNatives", Copy) { copyTask ->
    into "$buildDir/libs"
    from({configurations.windows_x64})
    configurations.windows_x64.asFileTree.each {
        copyTask.from(zipTree(it))
        copyTask.into("$buildDir/libs")
    }
}

test {
    dependsOn(tasks.named("extractNatives"))
    systemProperty("java.library.path", "$buildDir/libs")
    ignoreFailures = true
}

tasks.register("scaleTest", JavaExec) {
    mainClass = "gov.usbr.wq.merlindataexchange.ScaleTest"
    systemProperty("java.library.path", "$buildDir/libs")
    classpath = sourceSets.test.runtimeClasspath
}
